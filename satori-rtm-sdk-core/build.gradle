apply plugin: 'java'
apply plugin: 'maven'

description = "Satori Core SDK for Java."

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

sourceSets {
    integration {
        java {
            srcDir 'src/integration/java'
        }
        compileClasspath += sourceSets.test.compileClasspath
    }
}

configurations {
    integration
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name project.description
                description project.description
            }
        }
    }
}

task integrationTestJar(type: Jar, dependsOn: integrationClasses) {
    from sourceSets.integration.output
    description = 'Build the integration tests'
    group = 'verification'
    classifier = 'test'
}

artifacts {
    integration integrationTestJar
}

dependencies {
    compile group: 'com.neovisionaries', name: 'nv-websocket-client', version: '1.31'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.22'
    // needs to compile integration tests for Eclipse
    testCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    // needed to run tests from IDE
    integrationCompile project(path: ':satori-rtm-sdk-jackson2')
//TODO: We need to test it with both.
//    integrationCompile project(path: ':satori-rtm-sdk-gson')
}
